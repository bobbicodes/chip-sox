(defn duration
  "Takes a note value as the denominator of its fraction of a whole,
  e.g. 1 = whole, 2 = half, 4 = quarter, 6 = quarter-note-tuplets, etc,
  and the song tempo in quarter-notes-per-minute.
  Returns the note's length in seconds."
  [val tempo]
  (/ (/ 240 val) tempo))

(defn midi->freq
  "Takes a MIDI number (0-127) representing a note's pitch,
  outputs its frequency in Hz (cycles per second). Middle C is 60."
  [n]
  (* 440 (Math/pow 2 (/ (- n 69) 12))))

(defn init-wav!
  "Creates an empty .wav file to append notes to."
  []
  (shell/sh "sox" "-n" "-c" "1" "samples/temp0.wav" "trim" "0.0" "0.0"))

(defn create-note!
  "Generates one of the following waveforms:
  sine, square, triangle, sawtooth, trapezium, exp,
  [white] noise, tpdfnoise, pinknoise, brownnoise, pluck."
  [wave pitch val tempo]
  (shell/sh "mkdir" "-p" "samples")
  (shell/sh "sox" "-n"
            (str "samples/" wave "-" pitch "-" val "-" tempo ".wav")
            "synth" (duration val tempo) wave
            (str (midi->freq pitch))
            "trim" "0" (duration val tempo)))

(defn append-note!
  "Extends current track by concatenating a note to the end."
  [wave pitch val tempo]
  (shell/sh "sox" "samples/temp0.wav" 
            (str "samples/" wave "-" pitch "-" val "-" tempo ".wav")
            "samples/temp1.wav")
  (shell/sh "mv" "samples/temp1.wav" "samples/temp0.wav"))

(defn play! [file]
  (shell/sh "play" file))

(defn sample-exists? [wave pitch val tempo]
  (some #{(str "samples/" wave "-" pitch "-" val "-" tempo ".wav")}
        (str/split-lines (:out (shell/sh "ls")))))

(defn build-track! [file wave tempo notes]
  (init-wav!)
  (println (str "Building track - " file))
  (doseq [[pitch val] notes]
    (when-not (sample-exists? wave pitch val)
      (create-note! wave pitch val (str tempo)))
    (append-note! wave (str pitch) (str val) (str tempo)))
  (shell/sh "mv" "samples/temp0.wav" (str file ".wav")))

(comment
  (duration 16 175)
  (create-note! "triangle" 50 16 175)

  (build-track! "bass-1" "triangle" 175
                [[50 32] [46 32] [42 32] [40 32]
                 [36 4]])

  (play! "bass-1.wav")

  )
